dnl configure.in
dnl 
dnl Copyright 2001, 2002, 2003, 2004, 2005, 2006, 2007 The University of York
dnl Author: Paul Emsley
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2 of the License, or (at
dnl your option) any later version.
dnl 
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
dnl General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
dnl 02110-1301, USA
dnl
dnl This program is distributed in the hope that it will be useful, but
dnl WITHOUT ANY WARRANTY, to the extent permitted by law; without even the
dnl implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.
dnl
dnl Process this file with autoconf to produce a configure script.

# AC_INIT package-name version-number
AC_INIT(configure.in)
AM_INIT_AUTOMAKE(coot, 0.3.3-pre)
# for old versions of automake
# AM_CONFIG_HEADER(config.h)

dnl Pick up the macros.
AM_ACLOCAL_INCLUDE(macros)

AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC

AC_PROG_CXX
AM_PROG_LIBTOOL
AC_PROG_RANLIB
# AC_PROG_INTLTOOL, no, not today.  Mac compilation problems, no perl/ XML or something.

dnl test for WINDOWS_MINGW
AM_MINGW_WINDOWS

dnl Set platform-specific compilation flags - derived from clipper's configure
test "${target:+set}" = set || target="$host_os"
case "$target" in
   *linux* ) ;;
   *irix* )
	if test "`basename $CXX`" = CC; then
	   case "$CXXFLAGS" in 
	      *LANG::std* ) ;; 
              * )
		  CXXFLAGS="$CXXFLAGS -LANG:std -ptused"
           esac
           # This patches libtool... This change exists in libtool 1.4d and
           # is required such that -LANG: is not filtered out...
           AC_MSG_NOTICE([Patching libtool for irix CC])
           sed "s:no/\*-\*-irix\*):no/*-*-irix* | /*-*-irix*):" libtool > libtool.tp
           mv libtool.tp libtool
           chmod +x libtool
	fi
    ;;
esac           

dnl test for GSL
# min_gsl_version=1.3
AM_PATH_GSL(1.3,[COOT_GSL_FLAGS=-DHAVE_GSL], [COOT_GSL_FLAGS=""])
AC_SUBST(COOT_GSL_FLAGS)

AC_ARG_WITH(gtk2, [  use to compile coot with Gtk+2, rather than Gtk1 (default)],
 coot_gtk2="TRUE",
 coot_gtk2="FALSE")

if test $coot_gtk2 = TRUE ; then

     # GTK2 path:

  PKG_CHECK_MODULES(GTK, gtk+-2.0 >= 2.2.0)
  PKG_CHECK_MODULES(GNOME_CANVAS, libgnomecanvas-2.0 >= 0.15.0)
  if test -n "$GNOME_CANVAS_CFLAGS" ; then 
	GNOME_CANVAS_CFLAGS="-DHAVE_GNOME_CANVAS $GNOME_CANVAS_CFLAGS"
  fi
  AC_SUBST(GNOME_CANVAS_CFLAGS)
  
  # Temporary remove because I can't properly run autgen.sh on Paris
  # at Logan airport 23/07/06
  # No longer at Logan airport, so re-enable...
  #
  AM_PATH_GTKGLEXT_1_0(1.0.0,,[exit 1]) 

else 

   # GTK 1

   dnl AM_PATH_GLIB([MINIMUM-VERSION, [ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND [, MODULES]]]])
   dnl Test for GLIB, and define GLIB_CFLAGS and GLIB_LIBS, if "gmodule" or 
   dnl gthread is specified in MODULES, pass to glib-config
   dnl
   AM_PATH_GLIB(1.2.8,,[exit 1])

   dnl Check for GTKGL library
   AM_PATH_GTKGL(, [echo You need the gtkgl library provided by gtkglarea; exit 1],AC_MSG_ERROR([Cannot find proper gtkgl version]))

   dnl Check for gtk-canvas
   AM_PATH_GTKCANVAS

fi # coot_gtk2

dnl Check for GLUT
#
AM_PATH_GLUT(, [echo You need the GLUT utility library; exit 1], AC_MSG_ERROR([Cannot find proper GLUT version]))

dnl Check for MMDB.
AM_PATH_MMDB(, [echo Error locating necessary mmdb; exit 1],,)

AM_WITH_MMDBSSM

dnl Check for Clipper.  we exit if clipper is not found.
#
AM_PATH_CLIPPER(, [echo Error locating Clipper; exit 1],AC_MSG_ERROR([Cannot find Clipper]))

AM_WITH_GUILE(, [echo optional guile\?],,)
GUILE_FLAGS

AM_WITH_PYTHON

AM_WITH_GUILE_GTK
AM_PATH_GUILE_GTK

# #defines for SWIG:
if test $coot_gtk2 = TRUE ; then
  SWIG_FLAGS="-DCOOT_USE_GTK2_INTERFACE"
else 
  SWIG_FLAGS=""
fi
dnl echo ....... SWIG_FLAGS=$SWIG_FLAGS
AC_SUBST(SWIG_FLAGS)


dnl check for guile modules www and gtk here.

dnl check for GSL
dnl AC_CHECK_LIB(m,main)
dnl AC_CHECK_LIB(gslcblas,main)
dnl AC_CHECK_LIB(gsl,main)


AM_WITH_MYSQL_DATABASE


dnl Add the languages which your application supports here.
ALL_LINGUAS="es es_ES"
# AM_GNU_GETTEXT
GETTEXT_PACKAGE=coot
dnl we need to export ALL_LINGUAS here otherwise it is not expanded
dnl for GTk1 on cycle at least, so that we would get:
dnl No rule to make target `@ALL_LINGUAS@.po', needed by `@ALL_LINGUAS@ .gmo'.
AC_SUBST(ALL_LINGUAS)
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT

dnl For now: just include the declarations for config.h.in
AH_TEMPLATE([ENABLE_NLS], [Define if NLS support is used])
AH_TEMPLATE([PACKAGE_LOCALE_DIR], [Directory of locales])


dnl Set PACKAGE_LOCALE_DIR in config.h.
# if test "x${prefix}" = "xNONE"; then
  # AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
# else
  # AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
# fi

AC_OUTPUT([
Makefile
macros/Makefile
scheme/Makefile
python/Makefile
compat/Makefile
coot-utils/Makefile
ccp4mg-utils/Makefile
coords/Makefile
db-main/Makefile
density-contour/Makefile
surface/Makefile
coot-surface/Makefile
angles/Makefile
atom-utils/Makefile
mini-mol/Makefile
skeleton/Makefile
ligand/Makefile
geometry/Makefile
ideal/Makefile
build/Makefile
analysis/Makefile
sequence-view/Makefile	
src/Makefile
doc/Makefile
high-res/Makefile
tw/Makefile
setup/coot.sh	
setup/coot.csh
python/coot_load_modules.py
auxil/Makefile
auxil/catcheck/Makefile	
auxil/rama/Makefile	
auxil/sec-str/Makefile	
auxil/strand-analysis/Makefile	
auxil/vectors/Makefile	
])
# removed setup/Makefile
# po/Makefile.in

# GETTEXT related things: When AM_GNU_GETTEXT get enabled, these get enabled.
# po/Makefile
# intl/Makefile - sigh.  Fix this later, not now.


# or maybe:
#],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])

