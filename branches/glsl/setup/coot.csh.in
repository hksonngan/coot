# setup/coot.csh.in
# 
# Copyright 2001, 2002, 2003 The University of York
# Author: Paul Emsley
# 
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or (at
# your option) any later version.
# 
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
# 
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
# 02110-1301, USA




# ----------------------------------------------------------------------------
# ----------------------------------------------------------------------------
#      ARE YOU REALLY SURE YOU NEED TO BE EDITING THIS?
#      Only under very unusual circumstances should you need this file.
#      A more normal setup just addeds COOT_PREFIX/bin to the path, that
#      all you usually need.
# ----------------------------------------------------------------------------
# ----------------------------------------------------------------------------




# Coot things:
#
# Set COOT_PREFIX to be the directory into which you untarred coot:
# e.g. $HOME/coot/Linux-bubbles
#
setenv COOT_PREFIX "@prefix@"
#


# ----------------------------------------------------------------------------
# ----------------------------------------------------------------------------
# If the COOT_PREFIX is correct, then these should not need modification
# i.e.  nothing below here need be changed.
# ----------------------------------------------------------------------------
# ----------------------------------------------------------------------------
# 
set systype = `uname`
set prefix = $COOT_PREFIX
set path = ($COOT_PREFIX/bin $path)
unset prefix

if (! $?SYMINFO) then
   setenv SYMINFO             $COOT_PREFIX/share/coot/syminfo.lib
endif
setenv COOT_SCHEME_DIR     $COOT_PREFIX/share/coot/scheme
# setenv COOT_REFMAC_LIB_DIR $COOT_PREFIX/share/coot/lib
setenv GUILE_LOAD_PATH \
$COOT_PREFIX/share/guile/1.6:$COOT_PREFIX/share/guile:$COOT_PREFIX/share/guile/gtk-1.2:$COOT_PREFIX/share/guile/gui:$COOT_PREFIX/share/guile/www:$COOT_PREFIX/share/guile/site
setenv COOT_REF_STRUCTS $COOT_PREFIX/share/coot/reference-structures


setenv COOT_STANDARD_RESIDUES $COOT_PREFIX/share/coot/standard-residues.pdb
setenv COOT_RESOURCES_FILE    $COOT_PREFIX/share/coot/cootrc
setenv COOT_PIXMAPS_DIR       $COOT_PREFIX/share/coot/pixmaps
setenv COOT_DATA_DIR          $COOT_PREFIX/share/coot
setenv COOT_PYTHON_DIR        $COOT_PREFIX/share/coot/python
if ($?PYTHONPATH) then 
   setenv PYTHONPATH             $COOT_PREFIX/share/coot/python:${PYTHONPATH}
else 
   setenv PYTHONPATH             $COOT_PREFIX/share/coot/python
endif

switch ("$systype")

   case "Linux":
     if (! $?LD_LIBRARY_PATH) then
	  setenv LD_LIBRARY_PATH $COOT_PREFIX/lib
     else 
	  setenv LD_LIBRARY_PATH $COOT_PREFIX/lib:${LD_LIBRARY_PATH}
     endif
     breaksw

   case "Darwin":
     if (! $?DYLD_LIBRARY_PATH) then
	  setenv DYLD_LIBRARY_PATH $COOT_PREFIX/lib
     else 
	setenv DYLD_LIBRARY_PATH $COOT_PREFIX/lib:${DYLD_LIBRARY_PATH}
     endif
     breaksw

   case "IRIX":
     # we need gcc_s.so.1 which is in /usr/local/lib to run programs:
     #
     if (! $?LD_LIBRARYN32_PATH) then
        setenv LD_LIBRARYN32_PATH /usr/freeware/lib32:$COOT_PREFIX/lib:/usr/local/lib
     else 
        setenv LD_LIBRARYN32_PATH /usr/freeware/lib32:$COOT_PREFIX/lib:${LD_LIBRARYN32_PATH}:/usr/local/lib
     endif
     breaksw

   case "IRIX64":
     # we need gcc_s.so.1 which is in /usr/local/lib to run programs:
     #
     if (! $?LD_LIBRARYN32_PATH) then
        setenv LD_LIBRARYN32_PATH /usr/freeware/lib32:$COOT_PREFIX/lib:/usr/local/lib
     else 
        setenv LD_LIBRARYN32_PATH /usr/freeware/lib32:$COOT_PREFIX/lib:${LD_LIBRARYN32_PATH}:/usr/local/lib
     endif
     breaksw

endsw

